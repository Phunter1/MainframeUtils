	TITLE 'DTIME - DISPLAY TIME IN UPPER LEFT CORNER'
DTIME    CSECT
	USING DTIME,R15
	STM   R14,R4,SAVE
	LR    R4,R15
	DROP  R15
	USING DTIME,R4
	SVC   GETTIME  R0 = HOURS, MINUTES, SECONDS, 100TH SEC
	ST    R0,WORK
	SR    R0,R0
	IC    R0,WORK   R0=HOURS
	CVD   R0,PWORK
	MVC   DWORK,=X'40212020'
	ED    DWORK,PWORK+6
	MVC   DHH,DWORK+2
	IC    R0,WORK+1 R0=MINUTES
	CVD   R0,PWORK
	MVC   DWORK,=X'40212020'
	ED    DWORK,PWORK+6
	MVC   DMM,DWORK+2
	IC    R0,WORK+2 R0=SECONDS
	CVD   R0,PWORK
	MVC   DWORK,=X'40212020'
	ED    DWORK,PWORK+6
	MVC   DSS,DWORK+2
	MVI   DHH+2,C':'
	MVI   DMM+2,C':'
	LA    R1,DHHMMSS
	LA    R2,8
	SVC   EBCASC CONVERT TO ASCII
	LA    R0,X'0300' READ CURSOR - ADV. MS-DOS PG 403
	LA    R1,0      BX=0 PAGE #
	SVC   VIDEO     GET CURSOR ROW,COL IN R15
	ST    R15,SAVCUR
	LA    R0,X'0200' SET CURSOR - ADV. MS-DOS PG 402
	LA    R1,0                BX = 0 PAGE #
	L     R15,=A(ROW*256+COL) DX = ROW,COL FOR CURSUR SET
	SVC   VIDEO     SET CURSOR FOR DISPLAY
	LA    R2,8
	LA    R3,DHHMMSS
LOOP     EQU   *
	LA    R0,X'0E00'  WRITE TEXT IN TELETYPE MODE - ADV. MS-DOS PG 414
	IC    R0,0(R3)    CHAR TO DISPLAY
	LA    R3,1(R3)
	LA    R1,0        PAGE 0
	SVC   VIDEO
	BCT   R2,LOOP
	LA    R0,X'0200'  SET CURSOR
	L     R15,SAVCUR
	LA    R1,0
	SVC   VIDEO       RESTORE CURSOR
	LM    R14,R4,SAVE
	BR    R14
*
* PC/370 SUPR STATE SVC'S
*
EBCASC   EQU   13   CONVERT TO ASCII
GETTIME  EQU   18   GET TIME IN R0
VIDEO    EQU   128+16 ROM BIOS VIDEO DRIVER (TECH. REF. A-48)
SAVE     DC    9D'0'
SAVCUR   DC    F'0'
ROW      EQU   0    TOP LINE
COL      EQU   72   LAST 8 COLUMNS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4 BASE
R14      EQU   14
R15      EQU   15
DWORK    DC    CL4' Z99'
WORK     DC    F'0'
PWORK    DC    D'0'
DHHMMSS  DS    0CL8
DHH      DC    C'HH',C':'
DMM      DC    C'MM',C':'
DSS      DC    C'SS'
	END
